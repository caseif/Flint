buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

// apply Gradle plugins
plugins {
    id 'java'
    id 'maven'
    id 'eclipse'
    id 'idea'
    id 'checkstyle'
}

apply plugin: 'license'

defaultTasks 'clean', 'licenseFormat', 'build'

// Project information
group = 'net.caseif.flint'
version = '1.0-SNAPSHOT'

// Extended project information
ext.projectName = 'flint'
ext.description = 'A framework for (Minecraft) minigame mods.'
ext.inceptionYear = '2015'
ext.packaging = 'jar'
ext.author = 'Max Roncac√©'
ext.versionSuffix = version.contains('SNAPSHOT') ? (
        (
            System.getenv('GIT_COMMIT') ? ('-git('
            + (System.getenv('GIT_BRANCH').contains('/')
                    ? System.getenv('GIT_BRANCH').split('/')[1]
                    : System.getenv('GIT_BRANCH')) + '-'
            + System.getenv('GIT_COMMIT').substring(0, 7) + ')') : ''
        )
        + (System.getenv('BUILD_NUMBER') ? '-jnks' + System.getenv('BUILD_NUMBER') : '')
) : ''

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    deployerJars
}


// Project repositories
repositories {
    mavenCentral()
    maven { url 'http://repo.caseif.net/content/groups/public/' }
}

// Project dependencies
dependencies {
    compile 'com.google.guava:guava:17.0'
    compile 'org.apache.commons:commons-lang3:3.3.2'

    testCompile 'junit:junit:4.12'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.8'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    from 'LICENSE'
}

// License header formatting
license {
    header file('LICENSE')
    excludes([
            '**/*.properties',
            '**/*.yml'
    ])
    sourceSets = project.sourceSets
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

// check code style
checkstyle {
    configProperties = [
            'name'        : project.name,
            'year'        : project.inceptionYear
    ]
    configFile = file('etc/checkstyle.xml')
}

tasks.withType(Checkstyle){
    exclude '**/*.properties'
    exclude '**/*.yml'
}

jar.manifest.mainAttributes(
        'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        'Specification-Title': name,
        'Specification-Version': version + versionSuffix,
        'Specitication-Vendor': author
)

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        setUniqueVersion(true)
        configuration = configurations.deployerJars
        if (project.hasProperty('mavenRepo')) {
            repository(url: project.property('mavenRepo')) {
                authentication(userName: project.property('mavenUser'), password: project.property('mavenPass'))
            }
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.ext.projectName
            project {
                packaging 'jar'
                description project.ext.description
                url 'http://github.com/caseif/Flint'

                scm {
                    url 'https://github.com/caseif/Flint'
                    connection 'scm:git:git://github.com/caseif/Flint.git'
                    developerConnection 'scm:git:git@github.com:caseif/Flint.git'
                }

                licenses {
                    license {
                        name 'BSD 3-Clause'
                        url 'http://opensource.org/licenses/BSD-3-clause'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}
